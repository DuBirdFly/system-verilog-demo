// `include "uvm_macros.svh"
// import uvm_pkg::*;

`timescale 1ns/1ps

`default_nettype none
module tb_top;

    import uvm_pkg::*;
    import test_pkg::*;

    initial begin
        // note: 不应该使用 plusargs，因为不同 EDA 的系统函数不一样
        // note: 不应把 dump_wave 放在 uvm_test 里，否则很难访问到模块名
        // note：还有一种做法是用 ucli.key，但是这样访问具体信号比较麻烦，需要权衡
        // note：vip example 是放在 tb_top 里的，这里沿用 vip 的做法
        `ifdef DUMP_VCD
            $display("===============================================");
            $display("Dumping VCD waveforms");
            $dumpfile(test_pkg::VCS_FILENAME);
            $dumpvars(0);
            $display("===============================================");
        `endif

        `ifdef DUMP_FSDB
            $display("===============================================");
            $display("Dumping FSDB waveforms");
            $fsdbAutoSwitchDumpfile(test_pkg::FSDB_LIMIT_SIZE, test_pkg::FSDB_FILENAME, test_pkg::FSDB_FILE_AMOUNT);
            $fsdbDumpvars(0, tb_top, "+mda");
            // $fsdbDumpvarsToFile("dump_information.list");
            $display("===============================================");
        `endif
    end

    logic pclk;
    logic presetn;

    logic aclk;
    logic aresetn;

    clk_rst_gen #(
        .APB_FREQ       ( 20            ),
        .AXI_FREQ       ( 100           )
    )u_clk_rst_gen (
        .pclk           ( pclk          ),
        .presetn        ( presetn       ),

        .aclk           ( aclk          ),
        .aresetn        ( aresetn       )
    );

    apb_interface apb_if(
        .pclk           ( pclk          ),
        .presetn        ( presetn       )
    );

    axi_interface axi_if(
        .aclk           ( aclk          ),
        .aresetn        ( aresetn       )
    );

    wrapper_top u_wrapper_top (
        .apb            ( apb_if        ),
        .axi            ( axi_if        )
    );

    initial begin: config4uvm
        uvm_config_db#(virtual apb_interface)::set(null, "uvm_test_top", "apb", apb_if);
        uvm_config_db#(virtual axi_interface)::set(null, "uvm_test_top", "axi", axi_if);
        run_test();
    end

endmodule
`default_nettype wire
